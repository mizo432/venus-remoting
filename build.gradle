buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE+'
    }
    repositories {
        maven {
            url 'http://www.venus-pj.org/artifactory/plugins-release'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
        jcenter()

    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.13"
    }
}

apply plugin: 'base'
apply plugin: 'idea'
apply plugin: 'com.jfrog.artifactory'

repositories {
    maven {
        url 'http://www.venus-pj.org/artifactory/plugins-release'
    }
    mavenCentral()
    jcenter()
}

subprojects {
    group = 'org.venuspj'
    version = '1.0.0'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'jdepend'
    apply plugin: "findbugs"
    apply plugin: "maven"
    apply plugin: 'pmd'

    ext {
        //Library versions
        springBootVersion = '1.5.3.RELEASE+'
        springSecurityOauth2Version = '2.1.0.RELEASE+'
        guavaVersion = '21.0+'
        mybatisTypeHandlerVersion = '1.0.2'
        mybatisBootVersion = '1.2.0'
        assertJVersion = '3.6.2'
        h2dbVersion = '1.4.193+'
        springStateMachineCoreVersion = '1.2.1.RELEASE'
        logbackVersion = '1.1.9'
        slf4jVersion = '1.7.22'
        junitDevVersion = "4.11"
        junitVersion = "4.12"
        thymeleafExtrasVersion = '3.0.2.RELEASE+'
        thymeleafExtrasJava8timeVersion = '3.0.0.RELEASE+'
        springSessionVersion = '1.3.0.RELEASE+'
        findbugsAnnotationsVersion = '3.0.1'

        // build paramaters
        defaultEncoding = 'UTF-8'
    }

    tasks.withType(AbstractCompile)*.options*.encoding = defaultEncoding
    tasks.withType(GroovyCompile)*.groovyOptions*.encoding = defaultEncoding

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        maven {
            url 'http://www.venus-pj.org/artifactory/plugins-release/'
        }
        maven {
            url 'http://repo.opennms.org/maven2/'
        }
        jcenter()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }

    dependencies {
        compile group: 'com.google.code.findbugs', name: 'annotations', version: "${findbugsAnnotationsVersion}"
        testCompile group: 'org.assertj', name: 'assertj-core', version: "${assertJVersion}"
        testCompile(group: 'junit', name: 'junit', version: "${junitVersion}")
    }

    findbugs {
        // 失敗しても後続の処理を継続させる
        ignoreFailures = true
        sourceSets = [sourceSets.main]
        toolVersion = "3.0.1"
    }

    pmd {
        // 失敗しても後続の処理を継続させる
        ignoreFailures = true
        sourceSets = [sourceSets.main]
    }

    tasks.withType(Pmd) {
        reports {
            xml.enabled = true
        }
    }

// CPD（重複コードチェック処理）をCheckタスクに追加
    check.doLast {
        File outputDir = new File("$reportsDir/cpd/")
        outputDir.mkdirs()

        ant.taskdef(
                name: 'cpd',
                classname: 'net.sourceforge.pmd.cpd.CPDTask',
                classpath: configurations.pmd.asPath)

        ant.cpd(
                minimumTokenCount: '100',
                format: 'xml',
                encoding: defaultEncoding,
                outputFile: new File(outputDir, 'cpd.xml')
        ) {
            fileset(dir: "src/main/java") {
                include(name: '**/*.java')
            }
        }
    }

    test {
        // 失敗しても後続の処理を継続させる
        ignoreFailures = true
        reports {
            junitXml.enabled = true
        }
    }

    jacoco {
        toolVersion = '0.7.5.201505241946'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
        }

        // カバレッジレポートからテストクラスを除外
        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it, exclude: ['**/*Test.class'])
            })
        }
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '4.0'
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

task incrementVersionCode << {
    println(":incrementVersionCode - Incrementing Version Code...")
    def buildGradleFile = file("build.gradle")
    def patternVersionCode = Pattern.compile("versionCode (\\d+)")
    def buildGradleFileText = buildGradleFile.getText()
    def matcherVersionCode = patternVersionCode.matcher(buildGradleFileText)
    matcherVersionCode.find()
    def mVersionCode = Integer.parseInt(matcherVersionCode.group(1))
    def mNextVersionCode = mVersionCode + 1
    def manifestContent = matcherVersionCode.replaceAll("versionCode " + mNextVersionCode)
    println(":incrementVersionCode - current versionCode=" + mVersionCode);
    println(":incrementVersionCode - next versionCode=" + mNextVersionCode);
    buildGradleFile.write(manifestContent)
}

artifactory {
    publish {
        repository {
            repoKey = 'libs-release-local'
            maven = true

        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            maven = true

        }
    }
}
